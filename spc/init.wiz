//------------------------------------------------
// init.wiz
//------------------------------------------------
import "spc";
import "banks";

namespace init {

  inline func clear_dp (dp_tail : u8 in x) {
    do {
      *(x as *u8) = a;     // 4cy
      x--;            // 2cy
    } while !zero;   // 2/4 cy
  }


  let dsp_flag_initial = (spc.dsp.global.flag_reset | spc.dsp.global.flag_mute | spc.dsp.global.flag_ecen);
  let end_data = 0xff;
  let mvol = 127;
  const initial_data : [u8] = [
    spc.dsp.global.flags,    dsp_flag_initial,
    spc.dsp.global.mvol_l,   mvol,
    spc.dsp.global.mvol_r,   mvol,
    spc.dsp.global.evol_l,   0,
    spc.dsp.global.evol_r,   0,
    spc.dsp.global.key_on,   0,
    spc.dsp.global.key_off,  0,
    spc.dsp.global.echo_on,  0,
    spc.dsp.global.noise_on, 0,
    spc.dsp.global.pmod_on,  0,
    spc.dsp.global.edl,      0,
    spc.dsp.global.dir,      0,
    spc.dsp.global.esa,      0,

    end_data
  ];

  // require: x=0, dp=0
  inline func clear_dsp() {
    a = initial_data[x];
    while !negative {
      y = a;
      a = initial_data[++x];
      spc.dsp.data_and_addr = ya;
      x++;
      a = initial_data[x];
    };
  }
}
